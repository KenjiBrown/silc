#ifdef SILC_DIST_SERVER
#
#  apps/silcd/configure.ad
#
#  Author: Pekka Riikonen <priikone@silcnet.org>
#
#  Copyright (C) 2000 - 2005 Pekka Riikonen
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; version 2 of the License.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#

#ifdef SILC_DIST_TOOLKIT
if test x$without_silcd = xfalse; then
#endif SILC_DIST_TOOLKIT

# Logs directory
#
LOGSDIR="$silc_prefix/logs"
AC_ARG_WITH(logsdir,
  [[  --with-logsdir=DIR      directory for SILC Server logs [PREFIX/logs]]],
  [
    case "$withval" in
      no|yes)
        ;;
      *)
        LOGSDIR="$withval"
        ;;
    esac
  ])
AC_SUBST(LOGSDIR)
AC_DEFINE_UNQUOTED([SILC_LOGSDIR], "$LOGSDIR", [SILC_LOGSDIR])

# silcd config file checking
#
summary_silcd_configfile="/etc/silc/silcd.conf"
AC_ARG_WITH(silcd-config-file,
  [[  --with-silcd-config-file=FILE  use FILE as default configuration file
                                 for SILC Server [/etc/silc/silcd.conf]]],
    AC_DEFINE_UNQUOTED([SILC_SERVER_CONFIG_FILE], "$withval", [SILC_SERVER_CONFIG_FILE])
    summary_silcd_configfile="$withval"
  )

# silcd pid file checking
#
if test "x$localstatedir" != 'x${prefix}/var'; then
  PIDFILE="$localstatedir/silcd.pid"
else
  PIDFILE="$silc_prefix/var/silcd.pid"
fi

AC_ARG_WITH(silcd-pid-file,
  [[  --with-silcd-pid-file=FILE     use FILE as default pid file for SILC
                                 Server [/var/run/silcd.pid]]],
  [
    case "$withval" in
      no|yes)
        ;;
      *)
        PIDFILE="$withval"
        ;;
    esac
  ])
AC_SUBST(PIDFILE)

AC_CONFIG_FILES(
apps/silcd/Makefile
doc/example_silcd.conf
)

silcd="yes"
#ifdef SILC_DIST_TOOLKIT
fi	# without_silcd = false
#endif SILC_DIST_TOOLKIT
#endif SILC_DIST_SERVER
